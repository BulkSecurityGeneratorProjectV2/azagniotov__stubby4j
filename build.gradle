// vim: ft=groovy
import org.kt3k.gradle.plugin.coveralls.domain.CoberturaSourceReportFactory
ext.coberturaSourceReportFactory = new CoberturaSourceReportFactory()
project(':').ext.subprojectTestTotals = [:]
project(':').ext.subprojectTestTotals['total'] = 0

defaultTasks 'clean', 'build', 'allTestReport'
description = 'Gradle configuration for stubby4j'

buildscript {
   repositories {
      mavenCentral()
      jcenter()
   }

   dependencies {
      classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
      classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
   }
}

allprojects {
   apply plugin: 'java'
   apply plugin: 'idea'
   apply plugin: 'eclipse'
   project.buildDir = 'target'

   if (project.name != 'main') {
      apply from: "$rootDir/conf/gradle/console.gradle"
   }
}

apply from: "$rootDir/conf/gradle/ides.gradle"

subprojects {
   subproject ->
      sourceCompatibility = 1.7
      tasks.withType(JavaCompile) {
         doFirst {
            assert sourceCompatibility == "1.7": "The sourceCompatibility of $name was changed!"
         }
         options.encoding = 'UTF-8'
      }
      targetCompatibility = 1.7

      repositories {
         mavenCentral()
         jcenter()
         mavenLocal()
      }
      
      apply from: "$rootDir/conf/gradle/dependency.gradle"
}

task allTestReport(type: TestReport) {
   destinationDir = file("$buildDir/reports/all")
   reportOn subprojects*.test 
   doLast {
      def tabs = "\t\t\t";
      println ""
      println "       ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"
      for ( testProjectName in project(':').ext.subprojectTestTotals ) {   
         if (testProjectName.key == 'total') {
            continue;
         }         
         if (testProjectName.key == 'UNIT') {
            tabs += "\t"
         } else {
            tabs = "\t\t\t"
         } 
         println "       :::::      " +  testProjectName.key + tabs + "Passed (" + 
                                       testProjectName.value['success'] + "/" + 
                                       testProjectName.value['total']  + ")"       
      }
      println "       ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"
      println "       :::::          Total tests executed in $stubbyProjectName " + project(':').ext.subprojectTestTotals['total']
      println "       ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"
      println ""
   }
}

