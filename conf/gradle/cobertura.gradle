apply plugin: 'com.github.kt3k.coveralls'

configurations {
   coberturaRuntime { extendsFrom testRuntime }
}

dependencies {
   coberturaRuntime 'net.sourceforge.cobertura:cobertura:2.1.1'
}

task coberturaPrepare() {
   ext.datafileLocation = project(":main").buildDir.path + "/cobertura.ser"
    doLast {
      ant.taskdef(
         name: 'coberturaInstrument', 
         classname: 'net.sourceforge.cobertura.ant.InstrumentTask',
         classpath: configurations.coberturaRuntime.asPath
      )
     
      ant.taskdef(
         name: 'coberturaReport', 
         classname: 'net.sourceforge.cobertura.ant.ReportTask',
         classpath: configurations.coberturaRuntime.asPath
      )
   }
}

task coberturaInstrument(dependsOn: ['classes', 'coberturaPrepare']) {
   ext.outputDir = project(":main").buildDir.path + "/classes-instrumented"
   //ext.outputDir = "${sourceSets.main.output.classesDir}-instrumented"
   doLast {
      ant.coberturaInstrument(
         ignoreTrivial: true,
         todir: outputDir, 
         datafile: coberturaPrepare.datafileLocation) {
            ignoreMethodAnnotation(
               annotationName: "by.stub.annotations.CoberturaIgnore"
            )
            fileset(
               dir: project(':main').sourceSets.main.output.classesDir, 
               includes: "by/stub/client/**/*.class, by/stub/cli/CommandLineInterpreter.class, by/stub/database/*.class, by/stub/utils/*.class, by/stub/yaml/**/*.class, by/stub/handlers/AjaxHandler.class, by/stub/handlers/strategy/**/*.class",
               excludes: "**/*\$*.class"
         )
      }   
   }
}

task coberturaReport(dependsOn: ['coberturaInstrument', 'test'/*project(":unit").test*/]) << {
   ant.coberturaReport(
      format: "html", 
      destdir: project(":main").reporting.baseDir.path + "/cobertura/html",
      srcdir: "$rootDir/main/java", 
      datafile: coberturaPrepare.datafileLocation
   )

   ant.coberturaReport(
      format: "xml",
      destdir: project(":main").reporting.baseDir.path + "/cobertura/xml",
      srcdir: "$rootDir/main/java",
      datafile: coberturaPrepare.datafileLocation
   )
}

gradle.taskGraph.whenReady { graph ->
   if (graph.hasTask(cobertura)) { 
      tasks.withType(Test) {
         doFirst {
            systemProperty 'net.sourceforge.cobertura.datafile', coberturaPrepare.datafileLocation
            classpath = files(coberturaInstrument.outputDir) + classpath + configurations.coberturaRuntime
         }
      }
   }
} 

// Required for 'coveralls-gradle-plugin'
task testReport(type: TestReport, group: 'Build') {
  description = 'Points to an aggregated test report'
  destinationDir = file(project(":main").reporting.baseDir.path + "/cobertura/xml")
}

task cobertura {
   dependsOn = ['clean', 'coberturaReport']
}
